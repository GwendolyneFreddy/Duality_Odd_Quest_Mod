// Very similiar to Arimayus's script, puts up defensive spells
// Dispels enemy if need be, but for the most part, just wails on you

IF
	Global("duFighterMove","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("duFighterMove","LOCALS",1)
		SetGlobal("duCastingTimer","LOCALS",6)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_BLUR)
		MoveToObject("dumayor")
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy)
		Continue()
END

IF
	HaveSpell(WIZARD_STONE_SKIN)
	GlobalTimerExpired("duCastingTimer","LOCALS")
	CheckStatLT(Myself,2,STONESKINS)
THEN
	RESPONSE #100
		SetGlobalTimer("duCastingTimer","LOCALS",6)
		Spell(Myself,WIZARD_STONE_SKIN)
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

IF
	HaveSpell(WIZARD_MIRROR_IMAGE)
	GlobalTimerExpired("duCastingTimer","LOCALS")
	!StateCheck(Myself,STATE_MIRRORIMAGE)
THEN
	RESPONSE #100
		SetGlobalTimer("duCastingTimer","LOCALS",6)
		Spell(Myself,WIZARD_MIRROR_IMAGE)
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

IF
	HaveSpell(WIZARD_BREACH)
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("casterCastTime","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_DEAD)
	CheckStatGT(NearestEnemyOf(Myself),2,STONESKINS)		
THEN
	RESPONSE #100
		SetGlobalTimer("casterCastTime","LOCALS",6)
		Spell(NearestEnemyOf(Myself),WIZARD_BREACH)
		Continue()
END


IF
	GlobalTimerExpired("duCastingTimer","LOCALS")
	HaveSpell(WIZARD_TENSERS_TRANSFORMATION)
THEN
	RESPONSE #100
		SetGlobalTimer("duCastingTimer","LOCALS",6)
		Spell(Myself,WIZARD_TENSERS_TRANSFORMATION)
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

IF 
	HPPercentLT(Myself,20)	
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),50)	
		UseItem("POTN55",Myself)
		Wait(2)
END
	
IF
	!ActuallyInCombat()
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

// Potion of Super Healing
IF
	HPPercentLT(Myself,55)										
	HasItem("POTN55",Myself)						
THEN
	RESPONSE #100
		UseItem("POTN55",Myself)
		DisplayStringHead(Myself,@65)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END